apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = 'com.kadinche.example'
version = '0.0.1'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

afterEvaluate {
    publishing {
        publications {
            // Set for all build variant
            android.libraryVariants.all { variant ->
                "maven${variant.name.capitalize()}Aar"(MavenPublication) {
                    from components.findByName("${variant.name}")
                    if ("${variant.name}" != "release") {
                        version = "${version}-${variant.name}"
                    }
                }
            }

            // Set for each build variant
            /*
            debugAar(MavenPublication) {
                from components.debug
                version = "${version}-debug"
            }
            releaseAar(MavenPublication) {
                from components.release
            }
            */
        }
        repositories {
            maven {
                url "${rootProject.projectDir}/../UnityAndroidToasty/m2repository"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'], excludes: ['classes.jar'])
    compileOnly files('libs/classes.jar')
    implementation 'com.github.GrenderG:Toasty:1.4.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
