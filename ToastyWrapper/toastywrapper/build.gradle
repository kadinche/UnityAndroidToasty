apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = 'com.kadinche.example'
version = '0.0.2'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 23
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        multipleVariants('debug') {
            includeBuildTypeValues('debug')
        }
        multipleVariants('release') {
            includeBuildTypeValues('release')
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            // Set for all build variant
            android.libraryVariants.all { variant ->
                "maven${variant.name.capitalize()}Aar"(MavenPublication) {
                    from components.findByName("${variant.name}")
                    if ("${variant.name}" != "release") {
                        version = "${version}-${variant.name}"
                    }
                }
            }

            // Set for each build variant
            /*
            debugAar(MavenPublication) {
                from components.debug
                version = "${version}-debug"
            }
            releaseAar(MavenPublication) {
                from components.release
            }
            */
        }
        repositories {
            maven {
                url "${rootProject.projectDir}/../UnityAndroidToasty/m2repository"
            }
        }
    }
}

dependencies {
    compileOnly project(':unity')
    implementation 'com.github.GrenderG:Toasty:1.4.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
